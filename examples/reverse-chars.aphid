; reverse-chars.aphid
; TODO: creating reverse versions of charsets (and maybe other versions)
; might be common enough that we should add this to the prelude, or a
; standard library.

; create a new charset
:c64-rev make-charset

; ( bitmap -- bitmap' )
{ ; bitmap
  { flip-bit } map
} :reverse-bitmap defword

; ( bit -- 1-bit)
{ ; bit
  1          ; bit 1
  swap       ; 1 bit
  -
 } :flip-bit defword

; ( charname -- )
{ dup               ; charname charname
  :c64              ; charname charname :c64
  swap              ; charname :c64 charname
  get-hires-bitmap  ; charname [bits...]
  reverse-bitmap    ; charname [bits'...]
  :c64-rev          ; charname [bits...] :c64-rev
  ror               ; :c64-rev charname [bits...]
  set-hires-bitmap
} :make-reverse-char defword

:c64 get-charnames { make-reverse-char } for-each

:c64-upside-down make-charset

; ( charname -- )
{ dup               ; charname charname
  :c64              ; charname charname :c64
  swap              ; charname :c64 charname
  get-hires-bitmap  ; charname [bits...]
  dup               ; charname [bits...] [bits...]
  list-reverse      ; charname [bits...]
  :c64-upside-down  ; charname [bits...] :c64-upside-down
  ror               ; :c64-upside-down charname [bits...]
  set-hires-bitmap  
} :make-upside-down-char defword
; NOTE: this is *mostly* the same as `make-reverse-char`, but we need
; to dup the list here because `list-reverse` changes it in-place and
; does not push it to the stack.

:c64 get-charnames { make-upside-down-char } for-each

1 1 15 :c64 "NORMAL TEXT..." writexy*

1 3 15 :c64-rev "AND IN REVERSE!" writexy*

1 5 15 :c64-upside-down "UPSIDE DOWN... dizzy does it make me" string-reverse writexy*